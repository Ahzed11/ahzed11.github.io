---
title: "Hot code reloading in Erlang with Rebar3"
date: 2023-10-06T16:03:14+09:00
draft: true
tags: ["Erlang", "OTP", "Rebar3"]
---

After a lot of trial, error and some help from the Erlang community, I am finally able to do a hot code reload.

## Create a project
Creating a new project is as simple as
```sh
rebar3 new release
```

It will create and populate a folder named `myapp`. For the rest of this tutorial the `myapp` folder will be considered as being the root folder.

## Change the relx mode
Open the `rebar.config` file. And edit the `{mode, dev}` tuple which is part of the relx configuration to `{mode, prod}`. At the moment I am writing this tutorial, it is located at the eight line.

## Link the application with the supervisor and your gen_server

### Adding a gen_server
Create the file for the gen_server at `/apps/myapp/src/myapp_serv.erl`.
I will fill mine with this but you could put anything you want in it:

```erl
-module(myapp_serv).
-behaviour(gen_server).

%% API
-export([start_link/0]).
-export([hello/1]). % Added by myself
-export([init/1, handle_call/3, handle_cast/2, handle_info/2, terminate/2, code_change/3]).
-record(state, {}).
-define(SERVER, ?MODULE).


hello(Pid) ->
    gen_server:call(Pid, hello). % Added by myself

start_link() ->
    gen_server:start_link({local, ?SERVER}, ?MODULE, [], []).

init(_Args) ->
    {ok, #state{}}.

handle_call(hello, _From, State) -> % Added by myself
    {reply, hello, State};

handle_call(stop, _From, State) ->
    {stop, normal, stopped, State};

handle_call(_Request, _From, State) ->
    {reply, ok, State}.

handle_cast(_Msg, State) ->
    {noreply, State}.

handle_info(_Info, State) ->
    {noreply, State}.

terminate(_Reason, _State) ->
    ok.

code_change(_OldVsn, State, _Extra) ->
    {ok, State}.

```

Most of it was generated by the Erlang VScode extension. I have only added a function called `hello/1` which allows us to send an `hello` call to the server and it will reply us `hello` in return.

### Linking the supervisor and the gen_server
Here is you can link the supervisor with the gen_server.

```erl
%%%-------------------------------------------------------------------
%% @doc myapp top level supervisor.
%% @end
%%%-------------------------------------------------------------------

-module(myapp_sup).

-behaviour(supervisor).

-export([start_link/0]).

-export([init/1]).

-define(SERVER, ?MODULE).

start_link() ->
    supervisor:start_link({local, ?SERVER}, ?MODULE, []).

init([]) ->
  ChildSpec = #{id => myapp,
    start => {myapp_serv, start_link, []},
    restart => temporary,
    shutdown => 1000,
    type => worker,
    modules => [myapp_serv]},

  {ok, {#{strategy => simple_one_for_one,
    intensity => 3,
    period => 60},
    [ChildSpec]}
  }.


%% internal functions
```

I used a `simple_one_for_one` strategy because I like being able to spawn a new child as I wish however it is not a requirement.

## Compile your code and build a release it
To compile your code and release it, run this command: `rebar3 compile && rebar3 release`.

### Test your code
To run / test your release, type this command: `./_build/default/rel/myapp/bin/myapp-0.1.0 console`
After running this command, you should be in the Erlang shell. If you have copied my code, you should be able to run these command to check that everything is correct.

1. `{ok, Pid} = supervisor:start_child(myapp_sup, []).`
2.  `myapp_serv:hello(Pid).`

And you should get this answer `hello`.

I would suggest to keep this shell running while during the following steps as it will be easier to show that our update was correclty done.

## Adding a new functionality to our gen_server
As I want to keep it minimal, I will just add new function called `hey/1` that will behave the same as the `hey/1` function except that it will reply with `hey` instead of `hello`.

My gen_server now looks like this:
```erl
-module(myapp_serv).
-behaviour(gen_server).

%% API
-export([start_link/0]).
-export([hello/1, hey/1]).
-export([init/1, handle_call/3, handle_cast/2, handle_info/2, terminate/2, code_change/3]).
-record(state, {}).
-define(SERVER, ?MODULE).


hello(Pid) ->
    gen_server:call(Pid, hello).

hey(Pid) ->
    gen_server:call(Pid, hey).

start_link() ->
    gen_server:start_link({local, ?SERVER}, ?MODULE, [], []).

init(_Args) ->
    {ok, #state{}}.

handle_call(hello, _From, State) ->
    {reply, hello, State};

handle_call(hey, _From, State) ->
    {reply, hey, State};

handle_call(stop, _From, State) ->
    {stop, normal, stopped, State};

handle_call(_Request, _From, State) ->
    {reply, ok, State}.

handle_cast(_Msg, State) ->
    {noreply, State}.

handle_info(_Info, State) ->
    {noreply, State}.

terminate(_Reason, _State) ->
    ok.

code_change(_OldVsn, State, _Extra) ->
    {ok, State}.

```

### Update the version of the .app file
Open the `apps/myapp/src/myapp.app.src` file and replace the `"0.1.0"` version string with a higher version string. I will choose `"0.2.0"` for this example.

### Update the version of the rebar.config file
Open the `rebar.config` file and also replace the `"0.1.0"` version string with a higher version string. I will choose `"0.2.0"` for this example.

## Create an appup file
Create the appup file at `/apps/myapp/src/myapp.appup.src`.
Here, as the changes I have made are minimal, the appup file's content will be quite simple:
```
{"0.2.0", % New version
 [{"0.1.0", [{load_module, myapp_serv}]}], % steps for 0.1.0 -> 0.2.0
 [{"0.1.0", [{load_module, myapp_serv}]}]  % steps for 0.2.0 -> 0.1.0
}.
```
As I did not change the state of my gen_server I can simply load my gen_server's module.
If you do not know how to write an appup file, you can consult the [appup cookbook](https://www.erlang.org/doc/design_principles/appup_cookbook.html)

### Copy you appup file in your _build directory
Copy your `/apps/myapp/src/myapp.appup.src` to `_build/default/lib/myapp/ebin/myapp.appup` with the following command:
```sh
cp /apps/myapp/src/myapp.appup.src _build/default/lib/myapp/ebin/myapp.appup
```

## Create a relup file
Now, we want to create a relup file to upgrade our current release.
```sh
rebar3 relup -n myapp -v "0.2.0" -u "0.1.0"
```
This command tells rebar3 to generate the relup file necessary to go from `0.1.0` to `0.2.0`.

## Pack your release
To pack you release, use this command:
```sh
rebar3 tar -n myapp -v "0.2.0"
```

## Move it to your _build folder
`mv _build/default/rel/myapp/myapp-0.2.0.tar.gz _build/default/rel/myapp/releases/0.2.0/myapp.tar.gz`

## Upgrade your app
`_build/default/rel/myapp/bin/myapp-0.1.0 upgrade "0.2.0"`

## Test your upgrade
Call the `hey/1` function from the shell you have left open earlier.